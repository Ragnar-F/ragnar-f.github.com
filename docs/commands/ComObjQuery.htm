<!DOCTYPE HTML>
<html>
<head>
<title>ComObjQuery()</title>
<meta name="keywords" content="Befehl, Funktion, COM, Objekt">
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<link href="../static/font.css" rel="stylesheet" type="text/css" />
<script src="../static/ga.js" type="text/javascript"></script>
<!--[if lt IE 9]>
<script src="../static/html5.js"></script>
<![endif]-->
<script src="../navbar/NavScript.js" type="text/javascript"></script>
</head>
<body>
<script type="text/javascript">WriteHeader(1);</script>
<div id="content">
  <section>
    <div id="article">
    <h1>ComObjQuery() <span class="ver">[v1.0.96.00+]</span></h1>
    <p>Fragt die Schnittstelle oder den Dienst eines COM-Objekts ab.</p>
    <pre class="Syntax">InterfacePointer := ComObjQuery(ComObject, [SID,] IID)</pre>
    <h3>Parameter</h3>
    <table class="info">
      <tr>
        <td>ComObject</td>
        <td>Ein COM-Wrapper-Objekt oder unbearbeiteter Interface-Pointer.</td>
      </tr>
      <tr>
        <td>IID</td>
        <td>Eine Schnittstellenkennung (GUID) im Format "{xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx}".</td>
      </tr>
      <tr>
        <td>SID</td>
        <td>Eine Dienstkennung im gleichen Format wie IID. Beim Weglassen des Parameters entf&auml;llt auch das Komma.</td>
      </tr>
    </table>
    <h3>Allgemeine Bemerkungen</h3>
    <p>Im 2-Parameter-Modus ist diese Funktion vergleichbar mit <a href="http://msdn.microsoft.com/en-us/library/ms682521.aspx">IUnknown::QueryInterface</a>. Ist sowohl SID als auch IID angegeben, wird sie intern die <a href="http://msdn.microsoft.com/en-us/library/cc678965.aspx">IServiceProvider</a>-Schnittstelle abfragen und danach <a href="http://msdn.microsoft.com/en-us/library/cc678966.aspx">IServiceProvider::QueryService</a> aufrufen. In jedem Fall ist der R&uuml;ckgabewert entweder eine 0 oder ein Pointer auf die angeforderte Schnittstelle. Im Allgemeinen muss der Pointer <a href="ObjAddRef.htm">freigegeben</a> werden, sobald das Script damit fertig ist.</p>
    <h3>Verwandte Befehle</h3>
    <a href="ObjAddRef.htm">ObjRelease</a>, <a href="ComObjCreate.htm">ComObjCreate</a>, <a href="ComObjGet.htm">ComObjGet</a>, <a href="ComObjActive.htm">ComObjActive</a>, <a href="ComObjError.htm">ComObjError</a>
    <h3>Beispiele</h3>
    <pre id="ExClassName" class="NoIndent"><em>; Beispiel: Bestimmt den Klassennamen eines Objekts.</em>

obj := ComObjCreate("Scripting.Dictionary")

MsgBox % "Schnittstellenname: " ComObjType(obj, "name")

IID_IProvideClassInfo := "{B196B283-BAB4-101A-B69C-00AA00341D07}"

<em>; Fordert ein Pointer an, der auf die IProvideClassInfo-Schnittstelle des Objekts verweist.</em>
If !(pci := ComObjQuery(obj, IID_IProvideClassInfo))
{
    MsgBox IProvideClassInfo-Schnittstelle wird nicht unterst&uuml;tzt.
    Return
}

<em>; Ruft GetClassInfo auf, um einen Pointer auf die ITypeInfo-Schnittstelle zu ermitteln.</em>
DllCall(vtable(pci, 3), "ptr", pci, "ptr*", ti)

<em>; Ruft GetDocumentation auf, um den vollst&auml;ndigen Typennamen des Objekts zu ermitteln.</em>
DllCall(vtable(ti, 12), "ptr", ti, "int", -1, "ptr*", name, "ptr", 0, "ptr", 0, "ptr", 0)

<em>; Wandelt den BSTR-Pointer in einen brauchbaren String um.</em>
name := StrGet(name, "UTF-16")

<em>; Gibt unbearbeitete Interface-Pointer frei.</em>
ObjRelease(ti)
ObjRelease(pci)

<em>; Zeigt den Typennamen an!</em>
MsgBox % "Klassenname: " name

vtable(ptr, n) {
    <em>; NumGet(ptr+0) gibt die Adresse der virtuellen Funktionstabelle
    ; des Objekts zur&uuml;ck (kurz: vtable). Der Rest des Ausdrucks ermittelt
    ; die Adresse der n-ten Funktionsadresse von der vtable.</em>
    Return NumGet(NumGet(ptr+0), n*A_PtrSize)
}

</pre>
    <pre id="ExIE" class="NoIndent"><em>; Beispiel: Automatisiert ein vorhandenes Internet-Explorer-Fenster.</em>

sURL := "http://www.autohotkey.com/forum/"
If webBrowser := GetWebBrowser()
   webBrowser.Navigate(sURL)
Return

GetWebBrowser()
{
    <em>; Ermittelt einen unbearbeiteten Pointer auf das Dokument-Objekt des obersten IE-Fensters.</em>
    static msg := DllCall("RegisterWindowMessage", "str", "WM_HTML_GETOBJECT")
    SendMessage msg, 0, 0, Internet Explorer_Server1, ahk_class IEFrame
    If ErrorLevel = FAIL
        Return  ; IE nicht gefunden.
    lResult := ErrorLevel
    DllCall("oleacc\ObjectFromLresult", "ptr", lResult
        , "ptr", GUID(IID_IHTMLDocument2,"{332C4425-26CB-11D0-B483-00C04FD90119}")
        , "ptr", 0, "ptr*", pdoc)

    <em>; Fragt den WebBrowserApp-Dienst ab. Im diesen bestimmten Fall sind
    ; die SID und IID gleichwertig, das aber nicht immer so ist.</em>
    static IID_IWebBrowserApp := "{0002DF05-0000-0000-C000-000000000046}"
    static SID_SWebBrowserApp := IID_IWebBrowserApp
    pweb := ComObjQuery(pdoc, SID_SWebBrowserApp, IID_IWebBrowserApp)

    <em>; Gibt den Dokument-Objekt-Pointer frei.</em>
    ObjRelease(pdoc)

    <em>; Gibt das WebBrowser-Objekt zur&uuml;ck, das zuerst angepasst wird:</em>
    static VT_DISPATCH := 9, F_OWNVALUE := 1
    Return ComObject(VT_DISPATCH, pweb, F_OWNVALUE)
}

GUID(ByRef GUID, sGUID) <em>; Wandelt ein String in eine bin&auml;re GUID um und gibt deren Adresse zur&uuml;ck.</em>
{
    VarSetCapacity(GUID, 16, 0)
    Return DllCall("ole32\CLSIDFromString", "wstr", sGUID, "ptr", &amp;GUID) &gt;= 0 ? &amp;GUID : ""
}

</pre>
  </div>
  </section>
</div>
<script type="text/javascript">WriteFooter(1);</script>
</body>
</html>