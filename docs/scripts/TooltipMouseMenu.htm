<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
"http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>ToolTip-Mausmen&uuml; (ben&ouml;tigt XP/2k/NT) -- von Rajat</title>
<meta name="keywords" content="Script">
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1">
<link rel="alternate" type="application/rss+xml" title="AutoHotkey Forum RSS" href="/forum/rss.php">
<link href="../css/default.css" rel="stylesheet" type="text/css">
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-28224374-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</head>
<body>
<div id="wrapper">
<script src="../navbar/NavScript.js" language="JavaScript1.2" type="text/javascript"></script>
<script language="JavaScript1.2" type="text/javascript">WriteNavLink(1);</script>


<h1>ToolTip-Mausmen&uuml; (ben&ouml;tigt XP/2k/NT) -- von Rajat</h1>

<p>Dieses Script zeigt ein aufklappbares Men&uuml; beim kurzen Dr&uuml;cken der mittleren Maustaste an.  Ein Men&uuml;punkt kann mit einem Linksklick ausgew&auml;hlt werden. Das Men&uuml; wird geschlossen, sobald au&szlig;erhalb des Men&uuml;s mit der linken Maustaste geklickt wird.  Als aktuelle Verbesserung kann der Inhalt des Men&uuml;s ge&auml;ndert werden, abh&auml;ngig davon, welcher Fenstertyp aktiv ist (Notepad und Word wurden hier als Beispiele verwendet).
</p>
<p><a href="TooltipMouseMenu.ahk">Dieses Script herunterladen</a> &nbsp;| &nbsp;<a href="index.htm">Andere Beispiel-Scripts</a> &nbsp;| &nbsp;<a href="../index.htm">Home</a></p>

<pre class="NoIndent"><em>; Hier kann ein beliebiger Titel f&uuml;r das Men&uuml; angegeben werden:</em>
MenuTitle = -=-=-=-=-=-=-=-

<em>; Damit wird die Druckdauer der Maustaste bestimmt, bis das Men&uuml; angezeigt wird:</em>
UMDelay = 20

SetFormat, float, 0.0
SetBatchLines, 10ms
SetTitleMatchMode, 2
#SingleInstance


<em>;___________________________________________
;_____Men&uuml;-Definitionen_____________________</em>

<em>; Hier k&ouml;nnen die Men&uuml;punkte erstellt oder bearbeitet werden.
; Es d&uuml;rfen keine Leerzeichen im Schl&uuml;ssel-/Wert-/Bereichssnamen verwendet werden.</em>

<em>; Mach dir keine Sorgen &uuml;ber die Reihenfolge, das Men&uuml; wird sortiert.</em>

MenuItems = Editor/Rechner/Bereich 3/Bereich 4/Bereich 5


<em>;___________________________________________
;______Hier dynamische Men&uuml;punkte___________</em>

<em>; Syntax:
;     Dyn# = Men&uuml;punkt|Fenstertitel</em>

Dyn1 = MS Word|- Microsoft Word
Dyn2 = Editor II|- Editor

<em>;___________________________________________</em>

Exit


<em>;___________________________________________
;_____Men&uuml;bereiche__________________________</em>

<em>; Hier k&ouml;nnen die Men&uuml;bereiche erstellt oder bearbeitet werden.</em>

Editor:
Run, Notepad.exe
Return

Rechner:
Run, Calc
Return

Bereich3:
MsgBox, 3 ausgew&auml;hlt
Return

Bereich4:
MsgBox, 4 ausgew&auml;hlt
Return

Bereich5:
MsgBox, 5 ausgew&auml;hlt
Return

MSWord:
msgbox, Das ist ein dynamischer Eintrag (Word)
Return

EditorII:
msgbox, Das ist ein dynamischer Eintrag (Editor)
Return


<em>;___________________________________________
;_____Hotkey-Bereich________________________</em>

~MButton::
HowLong = 0
Loop
{
    HowLong ++
    Sleep, 10
    GetKeyState, MButton, MButton, P
    IfEqual, MButton, U, Break
}
IfLess, HowLong, %UMDelay%, Return


<em>; Dynamisches Men&uuml; vorbereiten</em>
DynMenu =
Loop
{
    IfEqual, Dyn%a_index%,, Break

    StringGetPos, ppos, dyn%a_index%, |
    StringLeft, item, dyn%a_index%, %ppos%
    ppos += 2
    StringMid, win, dyn%a_index%, %ppos%, 1000

    IfWinActive, %win%,
        DynMenu = %DynMenu%/%item%
}


<em>; Sortiertes Hauptmen&uuml; mit dynamisches Men&uuml; verbinden</em>
Sort, MenuItems, D/
TempMenu = %MenuItems%%DynMenu%


<em>; Fr&uuml;here Eintr&auml;ge entfernen</em>
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    MenuItem%a_index% =
}

<em>; Neue Eintr&auml;ge erstellen</em>
Loop, Parse, TempMenu, /
{
    MenuItem%a_index% = %a_loopfield%
}

<em>; Das Men&uuml; erstellen</em>
Menu = %MenuTitle%
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    numItems ++
    StringTrimLeft, MenuText, MenuItem%a_index%, 0
    Menu = %Menu%`n%MenuText%
}

MouseGetPos, mX, mY
HotKey, ~LButton, MenuClick
HotKey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
HotKey, ~LButton, Off
IfWinNotActive, %MenuTitle%
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <em>; Platz, bevor die erste Zeile startet</em>
mY /= 13    <em>; Ben&ouml;tigter Platz jeder Zeile</em>
IfLess, mY, 1, Return
IfGreater, mY, %numItems%, Return
StringTrimLeft, TargetSection, MenuItem%mY%, 0
StringReplace, TargetSection, TargetSection, %a_space%,, A
Gosub, %TargetSection%
Return

</pre></div>
</body>
</html>