<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
"http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>Einfache Fensterverschiebung (ben&ouml;tigt XP/2k/NT)</title>
<meta name="keywords" content="Script">
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1">
<link rel="alternate" type="application/rss+xml" title="AutoHotkey Forum RSS" href="/forum/rss.php">
<link href="../css/default.css" rel="stylesheet" type="text/css">
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-28224374-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</head>
<body>
<script src="../../NavScript.js" language="JavaScript1.2" type="text/javascript"></script>
<script language="JavaScript1.2" type="text/javascript">WriteNavLink(2);</script>


<h1>Einfache Fensterverschiebung (ben&ouml;tigt XP/2k/NT)</h1>

<p>Normalerweise kann ein Fenster nur verschoben werden, wenn die Titelleiste angeklickt wird. Dieses Script erweitert diese Funktion, sodass jeder Punkt in einem Fenster angeklickt werden kann. Um diesen Modus zu aktivieren, haltet die Feststelltaste oder die mittlere Maustaste beim Klicken gedr&uuml;ckt und verschiebt das Fenster danach zur gew&uuml;nschten Position.
</p>
<p><a href="EasyWindowDrag.ahk">Dieses Script herunterladen</a> &nbsp;| &nbsp;<a href="index.htm">Andere Beispiel-Scripts</a> &nbsp;| &nbsp;<a href="../index.htm">Home</a></p>

<pre class="NoIndent"><em>; Hinweis: Die Feststelltaste oder mittlere Maustaste kann optional losgelassen werden, nachdem
; die Maustaste gedr&uuml;ckt wurde, anstatt die Taste die ganze Zeit gedr&uuml;ckt zu halten.
; Dieses Script ben&ouml;tigt v1.0.25+.</em>

~MButton & LButton::
CapsLock & LButton::
CoordMode, Mouse  <em>; Auf Bildschirm-/absolute Koordinaten wechseln.</em>
MouseGetPos, EWD_MouseStartX, EWD_MouseStartY, EWD_MouseWin
WinGetPos, EWD_OriginalPosX, EWD_OriginalPosY,,, ahk_id %EWD_MouseWin%
WinGet, EWD_WinState, MinMax, ahk_id %EWD_MouseWin%
if EWD_WinState = 0  <em>; Nur, wenn das Fenster nicht maximiert ist.</em>
    SetTimer, EWD_WatchMouse, 10 <em>; Die Maus verfolgen, wenn sie vom Benutzer gezogen wird.</em>
return

EWD_WatchMouse:
GetKeyState, EWD_LButtonState, LButton, P
if EWD_LButtonState = U  <em>; Taste wurde losgelassen, daher ist das Ziehen abgeschlossen.</em>
{
    SetTimer, EWD_WatchMouse, off
    return
}
GetKeyState, EWD_EscapeState, Escape, P
if EWD_EscapeState = D  <em>; Escape wurde gedr&uuml;ckt, daher wird das Ziehen abgebrochen.</em>
{
    SetTimer, EWD_WatchMouse, off
    WinMove, ahk_id %EWD_MouseWin%,, %EWD_OriginalPosX%, %EWD_OriginalPosY%
    return
}
<em>; Ansonsten das Fenster neu positionieren, damit es den Mauskoordinaten entspricht,
; bedingt durch das Ziehen der Maus:</em>
CoordMode, Mouse
MouseGetPos, EWD_MouseX, EWD_MouseY
WinGetPos, EWD_WinX, EWD_WinY,,, ahk_id %EWD_MouseWin%
SetWinDelay, -1   <em>; Fenster wird schneller/fl&uuml;ssiger verschoben.</em>
WinMove, ahk_id %EWD_MouseWin%,, EWD_WinX + EWD_MouseX - EWD_MouseStartX, EWD_WinY + EWD_MouseY - EWD_MouseStartY
EWD_MouseStartX := EWD_MouseX  <em>; F&uuml;r den n&auml;chsten Timer-Aufruf auf diese Subroutine aktualisieren.</em>
EWD_MouseStartY := EWD_MouseY
return

</pre>
</body>
</html>